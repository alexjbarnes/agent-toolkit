# <!-- TODO: customise app_name to match your cmd/ directory -->
app_name := "your-app"

# Build variables
version := `git describe --tags --always --dirty 2>/dev/null || echo "dev"`
ldflags := "-X main.Version=" + version

# Build the binary into bin/
build:
    go build -ldflags "{{ldflags}}" -o bin/{{app_name}} ./cmd/{{app_name}}

# Run locally without building
run:
    go run -ldflags "{{ldflags}}" ./cmd/{{app_name}}

# Run all tests with race detector
test:
    go test -v -race ./...

# Run tests and generate an HTML coverage report (excludes generated files)
test-coverage:
    go test -coverprofile=coverage.raw ./...
    grep -v -E '_templ\.go|mock_.*\.go|\.pb\.go' coverage.raw > coverage.out
    go tool cover -func=coverage.out | tail -1
    go tool cover -html=coverage.out -o coverage.html
    @echo "Coverage report: coverage.html"

# Run linter
lint:
    golangci-lint run

# Run linter with auto-fix
lint-fix:
    golangci-lint run --fix

# Format code with gofumpt (falls back to gofmt if gofumpt is not installed)
fmt:
    @which gofumpt > /dev/null 2>&1 && gofumpt -w . || gofmt -w .

# Tidy module dependencies
tidy:
    go mod tidy

# Remove build artifacts
clean:
    rm -rf bin/ coverage.raw coverage.out coverage.html

# Build Docker image
docker-build:
    docker build -f docker/Dockerfile -t {{app_name}}:{{version}} .

# Cross-compile check
cross-compile:
    GOOS=darwin go build ./...
    GOOS=windows go build ./...
    GOOS=linux go build ./...

# Install pre-commit hook (TruffleHog secret scanning)
setup-hooks:
    cp scripts/pre-commit .git/hooks/pre-commit
    chmod +x .git/hooks/pre-commit
    @echo "Pre-commit hook installed"

# Tag and push a dev image with label (e.g. just dev-push mybranch)
dev-push label:
    git tag -f dev-{{label}}
    git push origin dev-{{label}} --force

# Tag and push a dev image with short commit SHA
dev-push-commit:
    #!/usr/bin/env bash
    sha=$(git rev-parse --short HEAD)
    git tag -f "dev-${sha}"
    git push origin "dev-${sha}" --force

# Pre-push check: lint, test, build, and cross-compile
check: lint test build cross-compile
